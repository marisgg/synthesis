// Simplified model of a mars rover
// original encoding by: Tim Quatmann and Sebastian Junges

pomdp

const int num_tasks = 4;

// tasks
hole double pr1 in {0.4..0.6:0.1};
hole double pr2 in {0.5..0.7:0.1};
hole double pr3 in {0.8..1.0:0.1};
hole double pr4 in {0.1..0.3:0.1};

module task
	// current task (0 means no task)
	task : [0..num_tasks] init 0;
	success : bool init false;
	
	[task1] task=0 -> pr1 : (task'=1) & (success'=true) + (1-pr1) : (task'=1) & (success'=false);
	[task2] task=0 -> pr2 : (task'=2) & (success'=true) + (1-pr2) : (task'=2) & (success'=false);
	[task3] task=0 -> pr3 : (task'=3) & (success'=true) + (1-pr3) : (task'=3) & (success'=false);
	[task4] task=0 -> pr4 : (task'=4) & (success'=true) + (1-pr4) : (task'=4) & (success'=false);
	[task_done] task>0 -> (task'= 0) & (success'=false);

endmodule


// energy

hole double energy_low in {0.98..0.99:0.01};
hole double energy_medium in {0.91..0.95:0.01};
hole double energy_high in {0.80..0.90:0.02}; 

module energy
	increased_energy : bool init false;
	
	[task1] true -> 1/2 : true + 1/2 : (increased_energy' = true);
	[task2] true -> 1/2 : true + 1/2 : (increased_energy' = true);
	[task3] true -> 1/2 : true + 1/2 : (increased_energy' = true);
	[task4] true -> 1/2 : true + 1/2 : (increased_energy' = true);

	[task_done] true -> (increased_energy' = false);
endmodule

// battery

formula low_energy_task = (task=1 | task=3);
formula medium_energy_task = (task=2);
formula high_energy_task = (task=4);

const int bat_max = 3;
formula empty = bat=0;
label "empty" = empty;
module battery
	bat: [0..bat_max] init bat_max;
	[task1] !empty -> true;
	[task2] !empty -> true;
	[task3] !empty -> true;
	[task4] !empty -> true;
	[task_done] low_energy_task & !increased_energy -> energy_low : true + (1-energy_low) : (bat'=bat-1);
	[task_done] (low_energy_task & increased_energy) | (medium_energy_task & !increased_energy) -> energy_medium : true + (1-energy_medium) : (bat'=bat-1);
	[task_done] (medium_energy_task & increased_energy) | high_energy_task -> energy_high : true + (1-energy_high) : (bat'=bat-1);
endmodule


// scientific value

formula low_value_task = (task=3);
formula medium_value_task = (task=1 | task=2);
formula high_value_task = (task=4);

hole int reward_low in {2..8:2};
hole int reward_medium in {10..19:3};
hole int reward_high in {20..40:10};

rewards "value"
	[task_done] low_value_task: reward_low;
	[task_done] medium_value_task: reward_medium;
	[task_done] high_value_task: reward_high;
endrewards

observable "increased_energy" = increased_energy;
observable "task" = task;
observable "success" = success;
observable "empty" = empty;
